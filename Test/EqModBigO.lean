import Mathlib

local macro_rules | `($x ^ $y)   => `(HPow.hPow $x $y)

-- TODO: Fix precedence in the `IsBigO` notation, and use the same fix for `EqModBigO`.

open Filter Topology Asymptotics Real

section functions_simps /- Uninteresting lemmas missing from Mathlib. -/

@[simp] lemma Pi.inv_pow_mul_inv_pow (a b : ‚Ñï) :
    ((fun n : ‚Ñï ‚Ü¶ 1 / (n : ‚Ñù)^a)*fun n : ‚Ñï ‚Ü¶ 1 / (n : ‚Ñù)^b) = (fun n : ‚Ñï ‚Ü¶ 1 / (n : ‚Ñù)^(a+b)) := by
  ext n
  field_simp
  ring

@[simp] lemma Pi.inv_mul_inv :
    ((fun n : ‚Ñï ‚Ü¶ 1 / (n : ‚Ñù)) * fun n : ‚Ñï ‚Ü¶ 1 / (n : ‚Ñù)) = (fun n : ‚Ñï ‚Ü¶ 1 / (n : ‚Ñù)^2) := by
  simpa using Pi.inv_pow_mul_inv_pow 1 1

lemma Pi.mul_eq {Œ± Œ≤ : Type*} [Mul Œ≤] (f g : Œ± ‚Üí Œ≤) : f*g = fun x ‚Ü¶ (f x * g x) := rfl

end functions_simps

section limits_and_computations /- Elementary limits and estimates missing from Mathlib. -/

lemma tendsto_inv_pow_pos {a : ‚Ñï} (ha : a ‚â† 0) :
    Tendsto (fun x : ‚Ñù ‚Ü¶ 1 / x^a) atTop (ùìù 0) := by
  have : ‚àÄ x : ‚Ñù, 1/x^a = x^(-a : ‚Ñ§) := by intro x; simp
  simp only [this, tendsto_pow_neg_atTop ha]

lemma tendsto_pow_div_log (a : ‚Ñ§) {b : ‚Ñï} (hb : b ‚â† 0) :
    Tendsto (fun x ‚Ü¶ (log x)^a / x^b) atTop (ùìù 0) := by
  have hb' := Nat.pos_of_ne_zero hb
  by_cases ha : a = 0
  ¬∑ simp_rw [ha, zpow_zero]
    exact tendsto_inv_pow_pos hb
  rw [‚Üê Asymptotics.isLittleO_iff_tendsto]
  ¬∑ simp only [‚Üê rpow_int_cast, ‚Üê rpow_nat_cast]
    apply isLittleO_log_rpow_rpow_atTop
    exact_mod_cast hb'
  ¬∑ intro x hx
    simp [(pow_eq_zero_iff hb').mp hx, zero_zpow a ha]

lemma tendsto_log_div_id  :
    Tendsto (fun x ‚Ü¶ log x / x) atTop (ùìù 0) := by
  simpa using tendsto_pow_div_log 1 one_ne_zero

lemma tendsto_log_div_id_nat_cast :
    Tendsto (fun n : ‚Ñï ‚Ü¶ log n / n) atTop (ùìù 0) :=
  tendsto_log_div_id.comp tendsto_nat_cast_atTop_atTop

lemma log_add_one (x : ‚Ñù) : log (x + 1) = log x + log (1 + 1/x) := by
  by_cases hx : x = 0
  ¬∑ simp [hx]
  by_cases hx' : x = -1
  ¬∑ simp [hx']
  rw [‚Üê log_mul hx, mul_add, mul_one, mul_div_cancel' 1 hx]
  field_simp
  simpa [eq_neg_iff_add_eq_zero] using hx'

lemma isLittleO_rpow_log_div_rpow (a c : ‚Ñù) {b d : ‚Ñù} (hbd : d < b) :
    (fun x ‚Ü¶ (log x)^a / x^b) =o[atTop] (fun x ‚Ü¶ (log x)^c / x^d) := by
  have A : ‚àÄ·∂† x in atTop, 0 < x ‚àß 0 < log x :=
    (eventually_gt_atTop 0).and (tendsto_log_atTop.eventually_gt_atTop 0)
  have : (fun x ‚Ü¶ log x ^ a / x ^ b / (log x ^ c / x ^ d)) =·∂†[atTop]
      fun x ‚Ü¶ log x ^ (a - c) / (x ^ (b - d)) :=
    A.mono fun x ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚Ü¶ by beta_reduce; rw [rpow_sub, rpow_sub, div_div_div_comm] <;> positivity
  rw [Asymptotics.isLittleO_iff_tendsto']
  ¬∑ apply Tendsto.congr' this.symm
    exact (isLittleO_log_rpow_rpow_atTop _ (sub_pos.2 hbd)).tendsto_div_nhds_zero
  ¬∑ refine A.mono fun _ _ ‚Ü¶ ?_
    simp [rpow_pos_of_pos, *, ne_of_gt]

lemma isLittleO_pow_log_div_pow (a c : ‚Ñï) {b d : ‚Ñï} (hbd : d < b) :
    (fun x ‚Ü¶ (log x)^a / x^b) =o[atTop] (fun x ‚Ü¶ (log x)^c / x^d) := by
  simp only [‚Üê rpow_nat_cast, isLittleO_rpow_log_div_rpow a c (Nat.cast_lt.mpr hbd)]

lemma isLittleO_log_div_pow {b d : ‚Ñï} (hbd : d < b) :
    (fun x ‚Ü¶ (log x) / x^b) =o[atTop] (fun x ‚Ü¶ (log x) / x^d) := by
  simpa using isLittleO_pow_log_div_pow 1 1 hbd

lemma isLittleO_inv_pow_pos_log {b d : ‚Ñï} (hbd : d < b) :
    (fun x : ‚Ñù ‚Ü¶ 1 / (x ^b)) =o[atTop] (fun x ‚Ü¶ (log x) / x^d) := by
  simpa using isLittleO_pow_log_div_pow 0 1 hbd

@[simp]
lemma seq_exp_log_eventually_eq : (fun (n : ‚Ñï) ‚Ü¶ exp (log n)) =·∂†[atTop] fun n ‚Ü¶ n := by
  apply (eventually_gt_atTop 0).mono (fun n hn ‚Ü¶ ?_)
  have hn' : (0 : ‚Ñù) < n := by exact_mod_cast hn
  simp [exp_log hn']

@[simp]
lemma eventually_mul_div_cancel (f : ‚Ñù ‚Üí ‚Ñù) :
    (id * fun x ‚Ü¶ f x/x)  =·∂†[atTop] f := by
  apply (eventually_gt_atTop 0).mono (fun n hn ‚Ü¶ ?_)
  field_simp ; ring

@[simp]
lemma eventually_mul_div_cancel_seq (f : ‚Ñï ‚Üí ‚Ñù) :
    ((fun n : ‚Ñï ‚Ü¶ (n:‚Ñù)) * fun n ‚Ü¶ f n/(n : ‚Ñù)) =·∂†[atTop] f := by
  apply (eventually_gt_atTop 0).mono (fun n hn ‚Ü¶ ?_)
  field_simp ; ring

open Asymptotics

lemma Filter.Tendsto.isBigO_mul_atTop {Œπ : Type*} {l : Filter Œπ} {f : Œπ ‚Üí ‚Ñù}
    (h : Tendsto f l atTop)  (g : Œπ ‚Üí ‚Ñù) : g =O[l] (f*g) := by
  apply IsBigO.of_bound'
  apply (tendsto_atTop.1 h 1).mono
  intro i hi
  simp only [norm_eq_abs, Pi.mul_apply, norm_mul]
  exact le_mul_of_one_le_left (abs_nonneg _) (hi.trans (le_abs_self _))

lemma Filter.Tendsto.isBigO_mul_nhds {Œπ : Type*} {l : Filter Œπ} {f : Œπ ‚Üí ‚Ñù} {x : ‚Ñù}
    (h : Tendsto f l (ùìù x)) (g : Œπ ‚Üí ‚Ñù) : (f*g) =O[l] g := by
  apply IsBigO.of_bound
  swap ; exact ‚Äñx‚Äñ + 1
  have hf' : Tendsto (fun i ‚Ü¶ ‚Äñf i‚Äñ) l (ùìù ‚Äñx‚Äñ) := norm h
  have : ‚àÄ·∂† i in l, |f i| ‚â§ |x| + 1 := by
    have : Set.Iic (‚Äñx‚Äñ + 1) ‚àà ùìù  ‚Äñx‚Äñ := Iic_mem_nhds (by linarith)
    exact hf' this
  apply this.mono
  intro i hi
  simp
  gcongr

lemma Filter.Tendsto.isBigO_mul_nhds' {Œπ : Type*} {l : Filter Œπ} {f : Œπ ‚Üí ‚Ñù} {x : ‚Ñù}
    (h : Tendsto f l (ùìù x)) (g : Œπ ‚Üí ‚Ñù) : (g*f) =O[l] g := by
  rw [mul_comm]
  exact h.isBigO_mul_nhds g

lemma Filter.Tendsto.isBigO_inv {Œπ : Type*} {l : Filter Œπ} {f g : Œπ ‚Üí ‚Ñù} (h : Tendsto f l atTop) :
    (1/g) =O[l] (f/g) := by
  apply IsBigO.of_bound'
  apply (tendsto_atTop.1 h 1).mono
  intro i hi
  simp only [Pi.div_apply, Pi.one_apply, norm_eq_abs, norm_div]
  gcongr
  rw [abs_one]
  exact hi.trans (le_abs_self _)

end limits_and_computations

attribute [simp] isBigO_refl EventuallyEq.rfl

/-! # Equality modulo BigO. -/

namespace Asymptotics
variable {Œ± : Type*} {E : Type*} {F : Type*} [SeminormedAddCommGroup E] [SeminormedAddCommGroup F]

/-- Placeholder for a tactic discharging easy asymptotic side conditions. -/
macro "asymp" : tactic => `(tactic| simp)

def EqModBigO (l : Filter Œ±) (h : Œ± ‚Üí F) (f g : Œ± ‚Üí E) : Prop := (f - g) =O[l] h

notation:50 f " = " g " + O[" l "](" h")" => EqModBigO l h f g

variable {l : Filter Œ±}

@[simp]
lemma eqModBigO_self (f : Œ± ‚Üí E) (h‚ÇÅ : Œ± ‚Üí F) : f = f + O[l](h‚ÇÅ) := by
  unfold EqModBigO
  simp
  apply isBigO_zero

lemma EqModBigO.congr {f f' g g' : Œ± ‚Üí E} {h h' : Œ± ‚Üí F} (hf : f = g + O[l](h))
    (hff' : f  =·∂†[l] f' := by simp) (hgg' : g  =·∂†[l] g' := by simp) (hhh' : h  =·∂†[l] h' := by simp) :
    f' = g' + O[l](h') :=
  (isBigO_congr (hff'.sub hgg') hhh').mp hf

lemma eqModBigO_of_eq {f g : Œ± ‚Üí E} {h‚ÇÅ : Œ± ‚Üí F} (eq : ‚àÄ x, f x = g x) :
    f = g + O[l](h‚ÇÅ) := by
  have : f - g = 0 := by ext x; simp [eq]
  simp [EqModBigO, this]
  apply isBigO_zero

lemma isBigO_of_eq {f g : Œ± ‚Üí E} (eq : ‚àÄ x, f x = g x) : f =O[l] g := by
  simp [show f = g from funext eq]

lemma EqModBigO.isBigO {f g : Œ± ‚Üí E} {h : Œ± ‚Üí F}
    (hfg : f = g + O[l](h)) (hg : g =O[l] h) : f =O[l] h := by
  simpa using (IsBigO.add hfg hg)

@[simp]
lemma EqModBigO.isBigO_zero {f : Œ± ‚Üí E} {h : Œ± ‚Üí F} :
    f = 0 + O[l](h) ‚Üî f =O[l] h := by
  simp [EqModBigO]

@[simp]
lemma EqModBigO.isBigO_zero' {f : Œ± ‚Üí E} {h : Œ± ‚Üí F} :
    f = (fun _ ‚Ü¶ 0) + O[l](h) ‚Üî f =O[l] h :=
  EqModBigO.isBigO_zero

lemma EqModBigO.isBigO' {f g : Œ± ‚Üí E} {h‚ÇÅ h‚ÇÇ : Œ± ‚Üí F}
    (hfg : f = g + O[l](h‚ÇÅ)) (hg : g =O[l] h‚ÇÇ)
    (h‚ÇÅh‚ÇÇ : h‚ÇÅ =O[l] h‚ÇÇ := by asymp) : f =O[l] h‚ÇÇ := by
  simpa using (IsBigO.trans hfg h‚ÇÅh‚ÇÇ).add  hg

/-! # Transitivity and basic calc block support -/
section Trans

lemma EqModBigO.trans {f g k : Œ± ‚Üí E} {h‚ÇÅ h‚ÇÇ h‚ÇÉ : Œ± ‚Üí F}
    (hfg : f = g + O[l](h‚ÇÅ)) (hgk : g = k + O[l](h‚ÇÇ))
    (h‚ÇÅh‚ÇÉ : h‚ÇÅ =O[l] h‚ÇÉ := by asymp) (h‚ÇÇh‚ÇÉ : h‚ÇÇ =O[l] h‚ÇÉ := by asymp) : f = k + O[l](h‚ÇÉ) := by
  unfold EqModBigO at *
  simpa using (hfg.trans h‚ÇÅh‚ÇÉ).add (hgk.trans h‚ÇÇh‚ÇÉ)

lemma EqModBigO.trans_tendsto {E F : Type*}
  [NormedAddCommGroup E] [NormedAddCommGroup F] {f g : Œ± ‚Üí E} {h : Œ± ‚Üí F}
  (hfg : f = g + O[l](h)) (hh : Tendsto h l (ùìù 0)) : Tendsto (f - g) l (ùìù 0) :=
IsBigO.trans_tendsto hfg hh

variable  (h : Œ± ‚Üí F)

instance : @Trans (Œ± ‚Üí E) (Œ± ‚Üí E) (Œ± ‚Üí E) (EqModBigO l h) (EqModBigO l h) (EqModBigO l h) where
  trans := EqModBigO.trans

instance : @Trans (Œ± ‚Üí E) (Œ± ‚Üí E) (Œ± ‚Üí E) (¬∑ =·∂†[l] ¬∑) (¬∑ = ¬∑ + O[l](h)) (¬∑ = ¬∑ + O[l](h)) where
  trans := by
    intro f f' f'' h‚ÇÅ h‚ÇÇ
    calc
      f - f'' =·∂†[l] f' -  f'' := h‚ÇÅ.sub (EventuallyEq.refl _ _)
      _ =O[l] h := h‚ÇÇ

instance : @Trans (Œ± ‚Üí E) (Œ± ‚Üí E) (Œ± ‚Üí E) (¬∑ = ¬∑ + O[l](h)) (¬∑ =·∂†[l] ¬∑) (¬∑ = ¬∑ + O[l](h)) where
  trans := by
    intro f f' f'' h‚ÇÅ h‚ÇÇ
    calc
      f - f'' =·∂†[l] f - f' := (EventuallyEq.refl _ _).sub h‚ÇÇ.symm
      _ =O[l] h := h‚ÇÅ

end Trans

/-! # Algebraic operation with EqModBigO. -/

lemma IsBigO.add_eqModBigO {f g : Œ± ‚Üí E} {h‚ÇÅ h‚ÇÇ : Œ± ‚Üí F}
     (hg : g =O[l] h‚ÇÅ) (h‚ÇÅh‚ÇÉ : h‚ÇÅ =O[l] h‚ÇÇ := by asymp) : f + g = f + O[l](h‚ÇÇ) := by
  simp [EqModBigO, hg.trans h‚ÇÅh‚ÇÉ]

lemma EqModBigO.add {f g f' g' : Œ± ‚Üí E} {h‚ÇÅ h‚ÇÇ h‚ÇÉ : Œ± ‚Üí F}
    (hfg : f = g + O[l](h‚ÇÅ)) (hfg' : f' = g' + O[l](h‚ÇÇ))
    (h‚ÇÅh‚ÇÉ : h‚ÇÅ =O[l] h‚ÇÉ) (h‚ÇÇh‚ÇÉ : h‚ÇÇ =O[l] h‚ÇÉ) :
    f + f' = g + g' + O[l](h‚ÇÉ) := by
  unfold EqModBigO at *
  convert (hfg.trans h‚ÇÅh‚ÇÉ).add (hfg'.trans h‚ÇÇh‚ÇÉ)
  dsimp
  abel

lemma EqModBigO.comp_tendsto {Œπ : Type*} {l' : Filter Œπ }{f g : Œ± ‚Üí E} {h : Œ± ‚Üí F}
    {u : Œπ ‚Üí Œ±} (hfg : f = g + O[l](h)) (hu : Tendsto u l' l) :
    f ‚àò u  = g ‚àò u  + O[l'](h ‚àò u) :=
  IsBigO.comp_tendsto hfg hu

section NormedSpace
variable {ùïú : Type*} [NormedField ùïú] {V : Type*} [SeminormedAddCommGroup V] [NormedSpace ùïú V]

lemma EqModBigO.smul {f g : Œ± ‚Üí ùïú} {f' g' : Œ± ‚Üí V} {h‚ÇÅ h‚ÇÇ h‚ÇÉ : Œ± ‚Üí ùïú}
    (hfg : f = g + O[l](h‚ÇÅ)) (hfg' : f' = g' + O[l](h‚ÇÇ))
    (h‚ÇÅg' : (h‚ÇÅ‚Ä¢g') =O[l] h‚ÇÉ) (h‚ÇÇg : (g*h‚ÇÇ) =O[l] h‚ÇÉ) (h‚ÇÅh‚ÇÇ : (h‚ÇÅ*h‚ÇÇ) =O[l] h‚ÇÉ) :
    f ‚Ä¢ f' = g ‚Ä¢ g' + O[l](h‚ÇÉ) := by
  unfold EqModBigO at *
  have : f ‚Ä¢ f' - g ‚Ä¢ g' = (f - g)‚Ä¢g' + g ‚Ä¢ (f'-g') + (f - g)‚Ä¢(f'-g') := by
    simp [sub_smul, smul_sub]
    abel
  rw [this]
  have h‚ÇÇgbis : (g ‚Ä¢ h‚ÇÇ) =O[l] h‚ÇÉ := by simp [h‚ÇÇg]
  exact ((hfg.smul $ isBigO_refl ..).trans h‚ÇÅg' |>.add <| ((isBigO_refl ..).smul hfg').trans h‚ÇÇgbis).add
    <| (hfg.smul hfg').trans h‚ÇÅh‚ÇÇ

lemma EqModBigO.mul {f g f' g' : Œ± ‚Üí ùïú} {h‚ÇÅ h‚ÇÇ h‚ÇÉ : Œ± ‚Üí ùïú}
    (hfg : f = g + O[l](h‚ÇÅ)) (hfg' : f' = g' + O[l](h‚ÇÇ))
    (h‚ÇÅg' : (h‚ÇÅ*g') =O[l] h‚ÇÉ) (h‚ÇÇg : (g*h‚ÇÇ) =O[l] h‚ÇÉ) (h‚ÇÅh‚ÇÇ : (h‚ÇÅ*h‚ÇÇ) =O[l] h‚ÇÉ) :
    f * f' = g * g' + O[l](h‚ÇÉ) :=
  hfg.smul hfg' h‚ÇÅg' h‚ÇÇg h‚ÇÅh‚ÇÇ

lemma EqModBigO.mul' {f g f' g' gg': Œ± ‚Üí ùïú} {h‚ÇÅ h‚ÇÇ h‚ÇÉ : Œ± ‚Üí ùïú}
    (hfg : f = g + O[l](h‚ÇÅ)) (hfg' : f' = g' + O[l](h‚ÇÇ))
    (h‚ÇÅg' : (h‚ÇÅ*g') =O[l] h‚ÇÉ) (h‚ÇÇg : (g*h‚ÇÇ) =O[l] h‚ÇÉ) (h‚ÇÅh‚ÇÇ : (h‚ÇÅ*h‚ÇÇ) =O[l] h‚ÇÉ)
    (hgg': gg'= g*g' := by try simp) :
    f * f' = gg' + O[l](h‚ÇÉ) := by
  rw [hgg']
  exact hfg.mul hfg' h‚ÇÅg' h‚ÇÇg h‚ÇÅh‚ÇÇ

end NormedSpace

section Calculus

/-! # EqModBigO from calculus. -/

variable {ùïú : Type*} [NontriviallyNormedField ùïú]
  {V : Type*} [NormedAddCommGroup V] [NormedSpace ùïú V]
  {W : Type*} [NormedAddCommGroup W] [NormedSpace ùïú W]

lemma _root_.HasFDerivAt.eqModBigO {f : V ‚Üí W} {f' : V ‚ÜíL[ùïú] W}
    {x‚ÇÄ : V} (hf : HasFDerivAt f f' x‚ÇÄ) : f = fun _ ‚Ü¶ f x‚ÇÄ + O[ùìù x‚ÇÄ]((¬∑ - x‚ÇÄ)) :=
  EqModBigO.isBigO (IsLittleO.isBigO hf) <| ContinuousLinearMap.isBigO_sub _ (ùìù x‚ÇÄ) x‚ÇÄ

lemma _root_.HasDerivAt.eqModBigO {f : ùïú ‚Üí V}
    {x‚ÇÄ : ùïú} {e : V} (hf : HasDerivAt f e x‚ÇÄ) : f = fun _ ‚Ü¶ f x‚ÇÄ + O[ùìù x‚ÇÄ]((¬∑ - x‚ÇÄ)) :=
  _root_.HasFDerivAt.eqModBigO hf


end Calculus
end Asymptotics

/-! # Applications -/

lemma exp_eqModBigO_zero : exp = 1 + O[ùìù 0](id) := by
  have : HasDerivAt exp 1 0 := by simpa using hasDerivAt_exp 0
  simpa using this.eqModBigO

lemma exp_inv_eqModBigO : (fun n : ‚Ñï ‚Ü¶ exp (1/n)) = 1 + O[atTop](fun n : ‚Ñï ‚Ü¶ 1/(n : ‚Ñù)) := by
   exact exp_eqModBigO_zero.comp_tendsto (tendsto_const_div_atTop_nhds_0_nat 1)

lemma Asymptotics.EqModBigO.exp_comp {Œ± : Type*} {l : Filter Œ±} {f g : Œ± ‚Üí ‚Ñù} {h : Œ± ‚Üí ‚Ñù}
    (hh : Tendsto h l (ùìù 0)) (hf : f = g + O[l](h)) : exp ‚àò f = exp ‚àò g + O[l]((exp ‚àò g) * h) := by
  have : exp ‚àò f = exp ‚àò g * (exp ‚àò (f - g))  := by
    ext x
    simp [‚Üê exp_add (g x) (f x - g x)]
  rw [this] ; clear this
  have hfg : Tendsto (f - g) l (ùìù 0) := by
    exact hf.trans_tendsto hh
  have F : rexp ‚àò (f - g) = 1 + O[l](f - g) := by simpa using exp_eqModBigO_zero.comp_tendsto hfg
  have : exp ‚àò (f - g) = 1 + O[l](h) := by
    exact IsBigO.trans F hf
  apply (eqModBigO_self (exp ‚àò g) (rexp ‚àò g * h)).mul' this
  simp only [isBigO_refl, mul_one]
  simp only [mul_comm, isBigO_refl]
  apply hh.isBigO_mul_nhds'

lemma isBigO_log_one_add : (fun x: ‚Ñù  ‚Ü¶ log (1 + x)) =O[ùìù 0] id := by
  have : HasDerivAt (fun x : ‚Ñù ‚Ü¶ 1+x) 1 0 := by
    simpa using (hasDerivAt_const (0 : ‚Ñù) (1 : ‚Ñù)).add (hasDerivAt_id' (0 : ‚Ñù))
  simpa using (this.log (by norm_num)).eqModBigO

/-- Notation for sequence avoiding the type ascription `fun n : ‚Ñï` where Lean would assume
a real number input. -/
notation "seq" n " ‚Ü¶ " f => fun n : ‚Ñï ‚Ü¶ f

lemma log_succ_eqModBigO :
    (seq n ‚Ü¶ log (n + 1)) = (seq n ‚Ü¶ log n) + O[atTop](seq n ‚Ü¶ 1/(n : ‚Ñù) ) := by
  have : (seq n ‚Ü¶ log (n + 1)) = (seq n ‚Ü¶ log n) + seq n ‚Ü¶ log (1 + 1/n) := by
    ext n; apply log_add_one
  let Inv := seq n ‚Ü¶ 1/(n : ‚Ñù)
  calc (seq n ‚Ü¶ log (n + 1))
      = (seq n ‚Ü¶ log n) + seq n ‚Ü¶ log (1 + 1/n) + O[atTop](Inv) := by simp [this]
    _ = (fun n: ‚Ñï ‚Ü¶ log n) + O[atTop](Inv) := by
          apply IsBigO.add_eqModBigO
          exact isBigO_log_one_add.comp_tendsto (tendsto_const_div_atTop_nhds_0_nat 1)
          simp -- don't understand why `simp` as auto-param doesn't do it.

lemma tao_aux :
    ((seq n ‚Ü¶ exp (1/n)) * seq n ‚Ü¶ log (n + 1)) =
    (fun n ‚Ü¶ log n) + O[atTop](seq n ‚Ü¶ (log (n : ‚Ñù))/n) := by
  apply exp_inv_eqModBigO.mul' log_succ_eqModBigO
  -- The next three side conditions should be discharged by a dedicated tactic.
  ¬∑ rw [Pi.mul_eq]
    apply isBigO_of_eq
    intro n
    field_simp
  ¬∑ simpa using (tendsto_log_atTop.comp tendsto_nat_cast_atTop_atTop).isBigO_inv
  ¬∑ rw [Pi.inv_mul_inv]
    apply IsLittleO.isBigO
    simpa using (isLittleO_inv_pow_pos_log one_lt_two).comp_tendsto tendsto_nat_cast_atTop_atTop

lemma tao : (seq n ‚Ü¶ (n + 1 : ‚Ñù) ^ (exp (1/n))) = seq n ‚Ü¶ n + O[atTop](seq n ‚Ü¶ log n) := by
  have := calc
    (seq n ‚Ü¶ (n + 1 : ‚Ñù) ^ (exp (1/n))) = (seq n ‚Ü¶ exp (exp (1/n) * log (n + 1))) := by
        ext n
        rw [rpow_def_of_pos, mul_comm]
        exact_mod_cast n.succ_pos
    _ = rexp ‚àò seq n ‚Ü¶ log n + O[atTop]((rexp ‚àò seq n ‚Ü¶ log n) * seq n ‚Ü¶ log n / n) :=
          tao_aux.exp_comp tendsto_log_div_id_nat_cast
    _ =·∂†[atTop] seq n ‚Ü¶ n := seq_exp_log_eventually_eq
  refine this.congr (by simp) (by simp) ?_
  calc
  ((seq n ‚Ü¶ rexp (log n)) * seq n ‚Ü¶ log n / n) =·∂†[atTop] (seq n ‚Ü¶ (n : ‚Ñù)) * seq n ‚Ü¶ log n / n :=
        seq_exp_log_eventually_eq.mul EventuallyEq.rfl
  _ =·∂†[atTop] seq n ‚Ü¶ log n := by simp
